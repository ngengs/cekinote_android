apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.ngengs.android.cekinote"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 10000003
        versionName "1.0.0"
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['RELEASE_STORE_LOCATION'])
            storePassword keystoreProperties['RELEASE_STORE_PASSWORD']
            keyAlias keystoreProperties['RELEASE_KEY_ALIAS']
            keyPassword keystoreProperties['RELEASE_KEY_PASSWORD']
        }
        beta {
            storeFile file(keystoreProperties['RELEASE_STORE_LOCATION'])
            storePassword keystoreProperties['RELEASE_STORE_PASSWORD']
            keyAlias keystoreProperties['RELEASE_KEY_ALIAS']
            keyPassword keystoreProperties['RELEASE_KEY_PASSWORD']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def newName = output.outputFile.name
                    newName = newName.replace("app-", applicationId + "-")
                    newName = newName.replace("-release", "-v" + versionName + "-build." + getBuildDate() + "-release")
                    output.outputFile = new File(output.outputFile.parent, newName)
                }
            }
        }
        beta {
            minifyEnabled true
            signingConfig signingConfigs.beta
            versionNameSuffix '-beta.3'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def newName = output.outputFile.name
                    newName = newName.replace("app-", applicationId + "-")
                    newName = newName.replace("-beta", "-v" + versionName + "-build." + getBuildDate())
                    output.outputFile = new File(output.outputFile.parent, newName)
                }
            }
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }
    }

    lintOptions {
        abortOnError false
    }
}

def getBuildDate() {
    def date = new Date().format('yyyyMMdd')
    return date
}

greendao {
    schemaVersion 1
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.afollestad.material-dialogs:core:0.9.1.0'
    compile 'com.afollestad.material-dialogs:commons:0.9.1.0'
    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:cardview-v7:25.1.0'
    compile 'com.android.support:design:25.1.0'
    compile 'org.greenrobot:greendao:3.2.0'
    compile 'com.jakewharton:butterknife:8.4.0'
    compile 'com.android.support:support-v4:25.1.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
}
